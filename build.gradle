apply plugin: 'java'
apply plugin: 'rpm'

sourceCompatibility = 1.7

version = '1.0'

jar.baseName = 'bdshr-terminology-server'

def tomcatVersion = "7.0.64";
def openmrsVersion = "1.11.4";

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:2.2.+'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'http://mavenrepo.openmrs.org/nexus/content/repositories/public'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

task extractWar(dependsOn: ['downloadMRSWar'], type: Copy) {
    from zipTree(file("${buildDir}/resources/main/openmrs.war"))
    into file("${buildDir}/openmrs")
}

def properties = new Properties()
properties.setProperty("SERVER_PORT", "9080")
properties.setProperty("BASE_DIR", "${buildDir}")
properties.setProperty("CONTEXT_PATH", "openmrs")
properties.setProperty("WAR_DIRECTORY", "${buildDir}/openmrs")
properties.setProperty("OPENMRS_RUNTIME_PROPERTIES_FILE", "${projectDir}/local/openmrs-runtime.properties")

task launch(dependsOn: ['build', 'extractWar'], type: JavaExec) {
    main "org.freeshr.terminology.launch.Main"
    classpath sourceSets.main.runtimeClasspath
    environment properties
}

configurations {
    mrsWar
}

jar {
    manifest {
        attributes 'Main-Class': "org.freeshr.terminology.launch.Main"
    }
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

task execute(dependsOn: 'build', type: JavaExec) {
    main = "org.freeshr.terminology.launch.Main"
    classpath = sourceSets.main.runtimeClasspath
    standardOutput = System.out
}

task normalizeJarName(type: Copy) {
    from "${buildDir}" + "/libs/"
    into "${buildDir}" + "/libs/"
    include(jar.baseName + '-' + version + '.jar')
    rename(jar.baseName + '-' + version + '.jar', 'bdshr-terminology-server.jar')
}

task dist(dependsOn: ['build','downloadMRSWar','normalizeJarName'], type: Rpm) {
    packageName 'bdshr-terminology-server'
    version '0.1'
    release project.hasProperty('release') ? project.release : '1'
    arch NOARCH
    os LINUX

    into '/opt/bdshr-terminology-server'

    requires('unzip')

    postInstall file("${projectDir}/scripts/postinstall.sh")
    preUninstall file("${projectDir}/scripts/preuninstall.sh")
    postUninstall file("${projectDir}/scripts/postuninstall.sh")

    from("${buildDir}/libs/bdshr-terminology-server.jar") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'lib/'
    }

    from("${projectDir}/resources/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'etc'
    }

    from("${projectDir}/scripts/rpm/") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'bin'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'run'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'log'
    }

    from("${buildDir}/resources/main/openmrs.war") {
        fileMode = 0755
        createDirectoryEntry = true
        into '.'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'modules'
    }

    from("${projectDir}/resources/placeholder") {
        fileMode = 0755
        createDirectoryEntry = true
        into 'openmrs'
    }


}

configurations {
    all*.exclude(group: 'com.sun.jdmk', module: 'jmxtools')
    all*.exclude(group: 'com.sun.jmx', module: 'jmxri')
    all*.exclude(group: 'javax.jms', module: 'jms')
    all*.exclude(group: 'javax.mail', module: 'mail')
}

task downloadMRSWar << {
    new File("${buildDir}/resources/main").mkdirs();
    configurations.mrsWar.resolve().each { file ->
        if (file.getName().contains("openmrs")) {
            file.renameTo(new File("${buildDir}/resources/main/openmrs.war"))
        }
    }
}

task updateMRSLogger << {
    copy {
        def zipFile = file("${buildDir}/resources/main/openmrs_std.war")
        def outputDir = file("${buildDir}/resources/main/openmrs")
        from zipTree(zipFile)
        into outputDir
    }

    copy {
        from "${projectDir}/resources/log4j.xml"
        into "${buildDir}/resources/main/openmrs/WEB-INF/classes/"
    }

    task ('rePackageOpenMRS', type: Zip) {
        from "${buildDir}/resources/main/openmrs"
        archiveName 'openmrs.war'
        destinationDir file("${buildDir}/resources/main/")
    }.execute()

    delete {
        delete "${buildDir}/resources/main/openmrs", "${buildDir}/resources/main/openmrs_std.war"
    }

}


task copyMRSLogFile(type: Copy) {
    from "${projectDir}/resources/log4j.xml"
    into "${buildDir}/resources/main/openmrs/WEB-INF/classes/"
}

dependencies {
    compile "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jasper:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jasper-el:${tomcatVersion}",
            "org.apache.tomcat:tomcat-jsp-api:${tomcatVersion}"
    mrsWar "org.openmrs.web:openmrs-webapp:${openmrsVersion}@war"
}